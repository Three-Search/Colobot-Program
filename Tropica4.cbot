extern void object::Tropica4()
{
	object o;
	
	while(radar(Target2)!=null)
	{
		fly(Target2);
		jet(0.1);
		wait(2);
		fly(TargetBot);
		jet(0.5);
		wait(1);
	}
	
	o = radar(SpaceShip);
	goto(o.position);
	takeoff();
}

public void object::fly(int c)
{
	object o = radar(c);
	fly(o);
}

public void object::fly(object o)
{
	float dis, dif,ang;
	
	while (o != null)
	{
		if(this.temperature < 0.8)
		{
			if (o == null)
			{
				break;
			}
			
			jet(throttle(this.position.z, this.velocity.z, o.position.z-1));
			pointAt(direction(o.position));
			dis = distance(position,o.position);
			if (o.category == TargetBot && dis < 30)
			{
				dif = o.position.z-position.z;
				ang = asin(dif/dis);
				aim(ang);
				fire(0.05);
			}
		}
		else
		{
			if (o == null)
			{
				break;
			}
			
			motor(0,0);
			jet(-0.4);
			while (this.temperature != 0)
			{
				wait(0.1);
			}
		}
		wait(0.1);
		if (o == null)
		{
			break;
		}
		else
		{
			if (distance(this.position,o.position) < 5)
			{
				break;
			}
		}
	}
}

public float throttle(float objSourceZ, float objSourceVelZ, float objTargetZ)
{
	if(objSourceZ + 5 < objTargetZ)
	{
		return 1;
	}
	else if(objSourceZ - 5 > objTargetZ)
	{
		return -1;
	}
	else
	{
		return (objTargetZ - objSourceZ - (objSourceVelZ/2))/5;
	}
}

public void pointAt(float dir)
{
	if(dir < -10)
	{
		motor(1,0);
	}
	else if(dir > 10 )
	{
		motor(0,1);
	}
	else
	{
		if(dir < 0)
		{
			float val = (dir/10)+1;
			motor(1, val);
		}
		else
		{
			float val = -(dir/10)+1;
			motor(val,1);
		}
	}
}
